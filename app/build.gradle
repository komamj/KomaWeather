apply plugin: 'com.android.application'

apply plugin: 'org.greenrobot.greendao'

def static releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion as int
    buildToolsVersion rootProject.ext.buildToolsVersion as String
    defaultConfig {
        applicationId "com.koma.weather"
        minSdkVersion rootProject.ext.minSdkVersion as int
        targetSdkVersion rootProject.ext.targetSdkVersion as int
        versionCode 1000
        versionName "1.0.00"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //签名配置
    signingConfigs {
        release {
            storeFile file(STORE_FILE_PATH)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            //不去除无用代码
            minifyEnabled false
            //不进行对齐
            zipAlignEnabled false
            //不去除无用资源
            shrinkResources false
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            // 去除无用代码
            minifyEnabled true
            // 进行对齐
            zipAlignEnabled true
            // 移除无用的资源
            shrinkResources true
            // 配置混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 配置签名
            signingConfig signingConfigs.release
        }


        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = "KomaWeather_v${defaultConfig.versionName}_${releaseTime()}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    productFlavors {
        fir {}

        umeng {}

        yingyongbao {}

        wandoujia {}

        baidu {}

        uc {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    lintOptions {
        abortOnError false
    }

    greendao {
        schemaVersion 1000
        targetGenDir 'src/main/java'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$rootProject.ext.support_version"
    compile "com.android.support:design:$rootProject.ext.support_version"
    compile "com.android.support.constraint:constraint-layout:$rootProject.ext.constraint_version"
    compile "com.android.support:cardview-v7:$rootProject.ext.support_version"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.support_version"
    testCompile 'junit:junit:4.12'

    compile "com.jakewharton:butterknife:$rootProject.ext.butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknife_version"
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxandroid_version"
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxjava_version"

    compile "com.github.bumptech.glide:glide:$rootProject.ext.glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.ext.glide_version"
    compile ("com.github.bumptech.glide:recyclerview-integration:$rootProject.ext.glide_version") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    //dagger2
    compile "com.google.dagger:dagger:$rootProject.ext.dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.dagger_version"

    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofit_version"

    compile "org.greenrobot:greendao:$rootProject.ext.greendao_version"

    /*compile "android.arch.persistence.room:runtime:$rootProject.ext.room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.ext.room_version"
    compile "android.arch.persistence.room:rxjava2:$rootProject.ext.room_version"*/

    compile 'com.umeng.analytics:analytics:latest.integration'
    //leakcanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.ext.leakcanary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.leakcanary_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.leakcanary_version"
}
